plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

val copyWebAssets by tasks.registering(Copy::class) {
    from(rootProject.file("../")) {
        include("index.html")
        include("manifest.webmanifest")
        include("css/**")
        include("js/**")
        include("vistas/**")
        include("iconos/**")
        include("imagenes/**")
        exclude("android-foreground-service/**")
    }
    into(layout.buildDirectory.dir("generated/assets/web"))
}

android {
    namespace = "com.carorur.tracker"
    compileSdk = 34

    defaultConfig {
        applicationId = "com.carorur.tracker"
        minSdk = 26
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    sourceSets {
        getByName("main") {
            assets.srcDir(layout.buildDirectory.dir("generated/assets"))
        }
    }
}

tasks.named("preBuild") {
    dependsOn(copyWebAssets)
}

dependencies {
    implementation("androidx.core:core-ktx:1.13.1")
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.activity:activity-ktx:1.9.2")
    implementation("androidx.webkit:webkit:1.11.0")

    implementation("com.google.android.gms:play-services-location:21.3.0")
}
